apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"

def keyProps = [:]
def jsonFile = file("${rootDir}/../src/settings/keys.json")

if (jsonFile.exists()) {
    keyProps = new groovy.json.JsonSlurper().parseText(jsonFile.text)
} else {
    println("⚠️ Warning: keys.json not found — release signing config may fail.")
}


android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.eggvinegarmixcalc"

    defaultConfig {
        applicationId "com.eggvinegarmixcalc"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file("${rootDir}/../${keyProps.SIGNING.STORE_FILE}")
            storePassword keyProps.SIGNING.STORE_PASSWORD
            keyAlias keyProps.SIGNING.KEY_ALIAS
            keyPassword keyProps.SIGNING.KEY_PASSWORD
        }
    }


    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false // Set to 'true' if you enable Proguard
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}

react {
    autolinkLibrariesWithApp()
}

def enableProguardInReleaseBuilds = false

def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

dependencies {
    implementation("com.facebook.react:react-android")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}

apply from: "../../node_modules/react-native-vector-icons/fonts.gradle"